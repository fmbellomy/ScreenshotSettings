plugins {
    id 'org.quiltmc.loom' version '1.0.+'
    // id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.1'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.+' // For loom 0.8-0.9, 0.11+
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}
sourceCompatibility = 17
targetCompatibility = 17

version = project.mod_version
group = project.maven_group

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name 'Quilt'
        url 'https://maven.quiltmc.org/repository/release'
    }
    // Mod Menu
    maven { url "https://maven.terraformersmc.com/releases/" }
    // YetAnotherConfigLib
    maven { url = "https://maven.isxander.dev/releases" }

}

dependencies {
    // To change the versions see the gradle.yml.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"

    modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
    modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"
    modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"

    // Mod Menu
    modApi "com.terraformersmc:modmenu:4.1.1"
    // YetAnotherConfigLib
    modImplementation "dev.isxander:yet-another-config-lib:1.7.1"
    // Iris Shaders integration
    modImplementation "maven.modrinth:iris:1.19.2-v1.4.3"
    // templating library for file naming system
    shadow(implementation('org.antlr:ST4:4.3.4'))



}
processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("*.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

java {
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}
shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev")
    relocate "org.antlr", "com.quantumgarbage.screenshotsettings.shadow.org.antlr"
}
remapJar {
    dependsOn(shadowJar)
    inputFile = tasks.shadowJar.archiveFile
}
jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
